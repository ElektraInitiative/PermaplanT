// Pipeline for releases, which is manually triggered on the jenkins webapp

// Function duplicated in `Jenkinsfile`
def node_info() {
    echo "Running stage on ${env.NODE_NAME}"
}

// Function duplicated in `Jenkinsfile` the command parameter
def wait_for_db() {
    retry(6) {
        sleep(time: 5, unit: 'SECONDS')
        sh '/usr/bin/pg_isready --host=db --username=ci --dbname=ci --timeout=10'
    }
}

stage('Build Schema') {
    // duplicated in `Build in Docker` stage
    node('docker') {
        node_info()

        checkout scm
        def rustImage = docker.build('permaplant-rust', './ci/container-images/permaplant-rust')

        docker.image('postgis/postgis:13-3.1').withRun('-e "POSTGRES_USER=ci" -e "POSTGRES_PASSWORD=ci"') { c ->
            rustImage.inside("--link ${c.id}:db -e 'DATABASE_URL=postgres://ci:ci@db/ci'") {
                checkout scm

                wait_for_db()

                sh './ci/build-scripts/build-schema.sh'

                stash includes: 'backend/src/schema.rs', name: 'schema.rs'
                stash includes: 'frontend/src/api_types/definitions.ts', name: 'definitions.ts'
            }
        }
    }
}

stage('Build in Docker') {
    def parallelBuild = [:]

    parallelBuild['build-backend'] = {
        // duplicated in `Build Schema` stage
        node('docker') {
            node_info()

            checkout scm
            def rustImage = docker.build('permaplant-rust', './ci/container-images/permaplant-rust')

            docker.image('postgis/postgis:13-3.1').withRun('-e "POSTGRES_USER=ci" -e "POSTGRES_PASSWORD=ci"') { c ->
                rustImage.inside("--link ${c.id}:db -e 'DATABASE_URL=postgres://ci:ci@db/ci'") {
                    checkout scm

                    wait_for_db()

                    sh 'cd backend && cargo build --release'

                    stash includes: 'backend/target/release/backend', name: 'backend'
                }
            }
        }
    }

    parallelBuild['build-frontend'] = {
        // duplicated in `Jenkinsfile.testAndBuildFrontend()`
        node('docker') {
            node_info()

            checkout scm
            def nodeImage = docker.build('permaplant-node', './ci/container-images/permaplant-node')

            nodeImage.inside {
                checkout scm
                unstash 'definitions.ts'

                stage('build-frontend') {
                    sh './ci/build-scripts/build-frontend.sh'
                    stash includes: 'frontend/dist/**/*', name: 'frontend'
                }

                stage('build-typedoc') {
                    sh 'cd frontend && npm run doc'
                    stash includes: 'frontend/src/generated/docs/', name: 'typedoc'
                }

                stage('build-storybook') {
                    sh './ci/build-scripts/build-storybook.sh'
                    stash includes: 'frontend/storybook-static/**/*', name: 'storybook'
                }
            }
        }
    }

    // duplicated in `Jenkinsfile.release.testAndBuildMdbook()`
    parallelBuild['build-mdbook'] = {
        node('docker') {
            node_info()

            checkout scm
            docker.build('permaplant-doc:build', './doc').inside {
                stage('build') {
                    sh 'mdbook build'
                    stash includes: 'book/', name: 'mdbook'
                }
            }
        }
    }

    parallel(parallelBuild)
}

// similar to `Deploy PR/Dev` stage in `Jenkinsfile`
stage('Deploy Prod') {
    node('permaplant && prod') {
        checkout scm

        echo 'Unstash Artifacts'
        unstash 'schema.rs'
        unstash 'frontend'
        unstash 'backend'
        unstash 'storybook'
        unstash 'typedoc'
        unstash 'mdbook'

        echo 'Deploying to Prod ...'
        sh "sudo /usr/local/bin/permaplant-deploy.sh prod ${WORKSPACE}"
    }
}
