def node_info() {
    echo "Running stage on ${env.NODE_NAME}"
}

def wait_for_db() {
    retry(6) {
        sleep(time: 5, unit: 'SECONDS')
        sh '/usr/bin/pg_isready --host=db --username=ci --dbname=ci --timeout=60'
    }
}

def abortPreviousRunUnlessMaster() {
  // We never want to abort builds of the master branch
  if (env.BRANCH_NAME=="master") return;
  def exec = currentBuild
             ?.rawBuild
             ?.getPreviousBuildInProgress()
             ?.getExecutor()
  if(exec) {
    exec.interrupt(
      Result.ABORTED,
      new CauseOfInterruption.UserInterruption(
        "Aborted by Build#${currentBuild.number}"
      )
    )
  }
}

def runDockerWithPostgresSidecar(String command, String stashsrc='', String stashdir='') {
    return {
        node('docker') {
            node_info()
            checkout scm
            def rust_image = docker.build("permaplant-rust", "./ci/container-images/permaplant-rust")
            docker.image('postgis/postgis:13-3.1').withRun('-e "POSTGRES_USER=ci" -e "POSTGRES_PASSWORD=ci"') { c ->
                rust_image.inside("--link ${c.id}:db -e 'DATABASE_URL=postgres://ci:ci@db/ci' -e 'BIND_ADDRESS_HOST=127.0.0.1' -e 'BIND_ADDRESS_PORT=8080' -e 'AUTH_DISCOVERY_URI=unused' -e 'AUTH_CLIENT_ID=unused' -e 'RUSTFLAGS=-D warnings' -e 'RUSTDOCFLAGS=-D warnings'") {
                    checkout scm
                    wait_for_db()
                    sh './ci/build-scripts/build-schema.sh'
                    // Because in Deploy the path for /target is a env in permaplant-deploy.sh, we can only solve it with prepending cd backend
                    sh "cd backend && ${command}"
                    if (stashsrc != null && stashdir != '') {
                        stash includes: stashsrc, name: stashdir
                    }
                }
            }
        }
    }
}

def testAndBuildMdbook() {
    return {
        node('docker') {
            checkout scm
            def mdbook_image = docker.build("permaplant-mdbook", "./ci/container-images/permaplant-mdbook")
            mdbook_image.inside {
                checkout scm
                stage('test-and-build'){
                    sh "mdbook test"
                    sh 'mdbook build'
                    stash includes: 'book/', name: 'mdbook'
                }
            }
        }
    }
}

def testAndBuildFrontend() {
    return {
        node('docker') {
            node_info()
            checkout scm
            def node_image = docker.build("permaplant-node", "./ci/container-images/permaplant-node")
            node_image.inside {
                checkout scm
                unstash 'definitions.ts'
                stage('test'){
                    dir("frontend") {
                        def checksum_before = sh(returnStdout: true, script: 'checksum package-lock.json').trim()
                        sh "npm i"
                        def checksum_after = sh(returnStdout: true, script: 'checksum package-lock.json').trim()
                        echo "Checking if package-lock.json is modified by npm install"
                        if (checksum_before != checksum_after) throw new PackageLockJsonModifiedException();
                        sh "npm ci"
                        sh "npm run format:check"
                        sh "npm run lint"
                        sh "npm run test"
                    }
                }

                stage('build'){
                    sh './ci/build-scripts/build-frontend.sh'
                    stash includes: 'frontend/dist/**/*', name: 'frontend'
                }

                stage('typedoc'){
                    sh 'cd frontend && npm run doc'
                    stash includes: 'frontend/src/generated/docs/', name: 'typedoc'
                }

                stage('storybook'){
                    sh './ci/build-scripts/build-storybook.sh'
                    stash includes: 'frontend/storybook-static/**/*', name: 'storybook'
                }
            }
        }
    }
}

// PIPELINE BEGIN
abortPreviousRunUnlessMaster()

// REQUIRED for Deployment
// see Jenkinsfile.release
stage('FMT check  Build Schema'){
    node('docker') {
        node_info()
        checkout scm
        def rust_image = docker.build("permaplant-rust", "./ci/container-images/permaplant-rust")
        docker.image('postgis/postgis:13-3.1').withRun('-e "POSTGRES_USER=ci" -e "POSTGRES_PASSWORD=ci"') { c ->
            rust_image.inside("--link ${c.id}:db -e 'DATABASE_URL=postgres://ci:ci@db/ci'") {
                checkout scm
                wait_for_db()
                withEnv(['RUSTFLAGS=-D warnings', 'RUSTDOCFLAGS=-D warnings']) {
                    // Workaround: we can't stop rustfmt from linting the generated schema.rs
                    dir("backend") {
                        sh "/bin/bash -c 'echo \"\" > src/schema.rs'"
                        sh "cargo fmt --check"
                    // End of Workaround
                    }
                    sh './ci/build-scripts/build-schema.sh'
                }
                stash includes: 'backend/src/schema.rs', name: 'schema.rs'
                stash includes: 'frontend/src/bindings/definitions.ts', name: 'definitions.ts'
            }
        }
    }
}

stage('Parallel Stage') {
    timeout(time: 2, unit: 'HOURS'){
        try {
            parallel(
                    "cargo-build": runDockerWithPostgresSidecar('cargo build --release && cargo clippy', 'backend/target/release/backend', 'backend'),
                    "cargo-check": runDockerWithPostgresSidecar('cargo check && cargo doc'),
                    "cargo-test": runDockerWithPostgresSidecar('cargo test'),
                    "frontend": testAndBuildFrontend(),
                    "mdbook": testAndBuildMdbook(),
                    failFast: true
            )
        } catch(err) {
            deleteDir();
            throw err;
        }
    }
}

stage('Deploy PR') {
    node('permaplant && pr') {
        node_info()

        checkout scm

        echo "Workspace is '${WORKSPACE}'"
        echo 'Ensure pr service is stopped'
        sh 'sudo /usr/sbin/service permaplant-pr stop'

        withEnv(['DATABASE_URL=postgres://cidb:cidb@127.0.0.1/cidb']) {
            echo 'Resetting the CI DB ...'
            sh 'sudo /usr/local/bin/permaplant-reset-ci.sh'

            echo "Unstash Artifacts"
            unstash 'schema.rs'
            unstash 'frontend'
            unstash 'backend'
            unstash 'storybook'
            unstash 'typedoc'
            unstash 'mdbook'

            echo 'Deploying to PR'
            sh "sudo /usr/local/bin/permaplant-deploy.sh pr ${WORKSPACE}"

            echo 'Loading Data to PR'
            dir('scraper') {
                sh 'npm ci'
                sh 'mkdir ./data/'
                sh 'cp /nextcloud/mergedDatasets.csv ./data/'
                sh 'cp /nextcloud/Companions.csv ./data/'
                sh 'cp /nextcloud/Antagonist.csv ./data/'
                sh 'cp /nextcloud/Sizes.csv ./data/'
                sh 'npm run insert'
                sh 'rm -rf ./data/'
                sh 'rm -rf ./node_modules/'
            }
        }
    }
}

// Deploying to Dev only happens on branch "master"
if (env.BRANCH_NAME == 'master') {
    try {
        stage('Deploy Dev') {
            node('permaplant && dev') {
                node_info()

                checkout scm

                echo "Unstash Artifacts"
                unstash 'schema.rs'
                unstash 'frontend'
                unstash 'backend'
                unstash 'storybook'
                unstash 'typedoc'
                unstash 'mdbook'

                echo 'Deploying to Dev ...'
                sh "sudo /usr/local/bin/permaplant-deploy.sh dev ${WORKSPACE}"
            }
        }
    } catch(Exception e) {
        // If master is failing we want to know ASAP so send a mail.
        // collect changes since last build
        def changes = currentBuild.changeSets.collect() {
        it.collect() {
            "* ${it.getCommitId().take(7)} - ${it.getAuthor()} - ${it.getMsg().take(40)}"
        }.join('\n')
        }.join('\n')
        if (!changes) {
        changes = "* No new changes since last build"
        }

        def message = """\
Build ${JOB_NAME}:${BUILD_NUMBER} failed.
Url: ${RUN_DISPLAY_URL}
Reason: ${e}

Changes: ${RUN_CHANGES_DISPLAY_URL}
${changes}

Logs: ${currentBuild.rawBuild.getLog(20).join('\n')}
"""
    mail subject: "Build ${JOB_NAME} failed",
        body: message,
        replyTo: "noreply@libelektra.org",
        to: "build@libelektra.org"
        throw err;
    }
}

// PRODUCTION DEPLOYMENT
// see Jenkinsfile.release

class PackageLockJsonModifiedException extends Exception {
    PackageLockJsonModifiedException() {
        super("package-lock.json was modified by npm install. Did you maybe use a wrong node version?")
    }
}
