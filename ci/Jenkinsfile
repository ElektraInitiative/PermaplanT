// This is the pipeline that gets executed for PR's and master changes.

// Imports
node('docker') {
    checkout scm
    utils = load('ci/utils.groovy')
    frontend = load('ci/steps/frontend.groovy')
    mdbook = load('ci/steps/mdbook.groovy')
    backend = load('ci/steps/backend.groovy')
    pre_commit = load('ci/steps/pre_commit.groovy')
}

// Main Pipeline
utils.main_wrapper(
{
    // Abort previous runs
    utils.abortPreviousRun()

    // Sanity Stage
    utils.parallel_stage('Sanity', [
        pre_commit.runPrecommit(),
        backend.cargoFmt(),
        backend.schemas(),
        backend.dieselMigrations(),
    ])

    // Test and Build Stage
    utils.parallel_stage('Test and Build', [
        backend.cargoBuild(),
        backend.cargoClippy(),
        backend.cargoDoc(),
        backend.cargoTest(),
        frontend.testAndBuildFrontend(),
        mdbook.testBuild(),
    ])


})

lock("${env.NODE_NAME}-exclusive") {
    stage('Deploy PR') {
        node('permaplant && pr') {
            node_info()

            checkout scm

            echo "Workspace is '${WORKSPACE}'"
            echo 'Ensure permaplant service is stopped'
            sh 'sudo /usr/sbin/service permaplant stop'

            withEnv(['DATABASE_URL=postgres://permaplant:permaplant@127.0.0.1/permaplant']) {
                echo 'Resetting the CI DB ...'
                sh 'sudo /usr/local/bin/permaplant-reset-ci.sh'

                echo 'Unstash Artifacts'
                unstash 'schema.rs'
                unstash 'frontend'
                unstash 'backend'
                unstash 'storybook'
                unstash 'typedoc'
                unstash 'mdbook'

                echo 'Deploying to PR'
                sh "sudo /usr/local/bin/permaplant-deploy.sh pr ${WORKSPACE}"

                echo 'Loading Data to PR'
                dir('scraper') {
                    sh 'npm ci'
                    sh 'mkdir ./data/'
                    sh 'cp /nextcloud/mergedDatasets.csv ./data/'
                    sh 'cp /nextcloud/Companions.csv ./data/'
                    sh 'cp /nextcloud/Antagonist.csv ./data/'
                    sh 'cp /nextcloud/Sizes.csv ./data/'
                    sh 'npm run insert'
                    sh 'rm -rf ./data/'
                    sh 'rm -rf ./node_modules/'
                }
            }
        }
    }

    stage('E2E Tests') {
        node('docker') {
            node_info()
            checkout scm
            def e2eImage = docker.build("permaplant-e2e:build", "./e2e")
            try {
                e2eImage.inside("-e E2E_URL=https://pr.permaplant.net") {
                    wait_for_pr_db()
                    sh 'make test-e2e'
                }
            } catch (err) {
                echo "Error occurred during the e2eImage.inside block: ${err}"
                // rethrow so build fails
                // groovylint-disable-next-line
                throw err;
            } finally {
                archiveArtifacts artifacts: 'e2e/test-results/**', fingerprint: true,  allowEmptyArchive: true
                archiveArtifacts artifacts: 'e2e/test-reports/report.html', fingerprint: true,  allowEmptyArchive: true
                cucumber failedFeaturesNumber: -1, failedScenariosNumber: -1, failedStepsNumber: -1, \
                fileIncludePattern: 'e2e/test-reports/cucumber.json', pendingStepsNumber: -1, skippedStepsNumber: -1, \
                sortingMethod: 'ALPHABETICAL', undefinedStepsNumber: -1
                deleteDir();
            }
        }
    }
}

// Deploying to Dev only happens on branch "master"
if (env.BRANCH_NAME == 'master') {
    try {
        stage('Deploy Dev') {
            node('permaplant && dev') {
                node_info()

                checkout scm

                echo 'Unstash Artifacts'
                unstash 'schema.rs'
                unstash 'frontend'
                unstash 'backend'
                unstash 'storybook'
                unstash 'typedoc'
                unstash 'mdbook'

                echo 'Deploying to Dev ...'
                sh "sudo /usr/local/bin/permaplant-deploy.sh dev ${WORKSPACE}"
            }
        }
    } catch (Exception e) { // groovylint-disable-line
        // If master is failing we want to know ASAP so send a mail.
        // collect changes since last build
        def changes = currentBuild.changeSets.collect {
            it.collect {
                "* ${it.getCommitId().take(7)} - ${it.getAuthor()} - ${it.getMsg().take(40)}"
        }.join('\n')
        }.join('\n')
        if (!changes) {
            changes = '* No new changes since last build'
        }

        def message = """\
Build ${JOB_NAME}:${BUILD_NUMBER} failed.
Url: ${RUN_DISPLAY_URL}
Reason: ${e}

Changes: ${RUN_CHANGES_DISPLAY_URL}
${changes}

Logs: ${currentBuild.rawBuild.getLog(20).join('\n')}
"""
        mail subject: "Build ${JOB_NAME} failed",
        body: message,
        replyTo: 'noreply@libelektra.org',
        to: 'build@libelektra.org'
        throw err
    }
}
